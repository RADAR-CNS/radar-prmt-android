apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId 'org.radarcns.detail'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 54
        versionName '1.0.15'
        manifestPlaceholders = ['appAuthRedirectScheme': 'org.radarbase.passive.app']
        multiDexEnabled true
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "channel"
    productFlavors {
        playStore {
            dimension "channel"
            versionNameSuffix "-playStore"
        }
        selfRelease {
            dimension "channel"
            versionNameSuffix "-selfRelease"
        }
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    flatDir { dirs 'libs' }
    google()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.thehyve.nl/content/repositories/releases' }
}

ext.radarCommonsVersion = '1.1.3'

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.radarbase:radar-commons-android:$radarCommonsVersion"

    implementation "org.radarbase:radar-android-login-qr:$radarCommonsVersion"
    implementation 'com.google.firebase:firebase-core:18.0.3'
    implementation 'com.google.firebase:firebase-config:20.0.4'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'org.slf4j:slf4j-api:1.7.30'

    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation 'com.google.firebase:firebase-analytics:18.0.3'

    implementation 'com.gitlab.mvysny.slf4j:slf4j-handroid:1.7.30'

    implementation "org.radarbase:radar-android-phone:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-phone-usage:$radarCommonsVersion"
    selfReleaseImplementation "org.radarbase:radar-android-phone-telephony:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-empatica:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-application-status:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-weather:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-audio:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-faros:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-ppg:$radarCommonsVersion"

    implementation 'androidx.lifecycle:lifecycle-process:2.3.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.30'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
        apiVersion = '1.4'
        languageVersion = '1.4'
    }
}

// Needed for Firebase. Put at the bottom so it can detect the Firebase version.
apply plugin: 'com.google.gms.google-services'
